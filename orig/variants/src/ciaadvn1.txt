10 CLS:CLEAR250:DEFINTA-Z:GOTO 40
20 Z9=0:FOR R=1 TO 50:IF V1$=N$(R) AND (N(R)=LO OR N(R)=-1) THEN Z9=1:RETURN
30 NEXT:PRINT"I DON'T SEE THAT HERE.":Z9=2:RETURN
40 PRINTCHR$(23);"        C.I.A  ADVENTURE":PRINT"      BY   HUGH   LAMPERT"
50 DIM N1$(50),N$(50),N(50),V$(18),LO(30,4),LO$(30):FL=1:DR=0:DG=-1:T=0:EL=0:BU=0:DO=0:CA=0:BA=0:TE=0:GL=0
60 PA=0:RO=0:FOR R=1 TO 5:R1=RND(9):CO$=CO$+STR$(R1):NEXT:LO=1:A9$=">"+STRING$(62,"-")+"<"
70 R=LEN(CO$):CO$=RIGHT$(CO$,R-1)
80 FOR R=1 TO 50:READ N1$(R),N$(R),N(R):NEXT
90 FOR R=1 TO 30:READ LO$(R),LO(R,1),LO(R,2),LO(R,3),LO(R,4):NEXT
100 FOR R=1 TO 18:READ V$(R):NEXT
110 CLS
120 PRINT"WE ARE ";LO$(LO);"."
130 FOR R=1 TO 46:IF N(R)=LO PRINT"I CAN SEE ";N1$(R);"."
140 NEXT:B=0:FOR R=1 TO 4:IF LO(LO,R)>0 THEN B=1
150 NEXT:IF B=0 THEN 200
160 PRINT"WE COULD EASILY GO: ";:IF LO(LO,1)>0 PRINT"NORTH  ";
170 IF LO(LO,2)>0 PRINT"SOUTH  ";
180 IF LO(LO,3)>0 PRINT"EAST  ";
190 IF LO(LO,4)>0 PRINT"WEST  ";
200 PRINT:PRINTA9$
210 T=T+1:IF DG>0 THEN DG=DG-1
220 IF T=1 INPUT"ENTER YOUR NAME PARTNER";NA$
230 GOTO 1760
240 N=0:FOR R=1 TO 46:IF N(R)=-1 THEN N=N+1
250 NEXT
260 PRINT:INPUT"WHAT DO YOU THINK WE SHOULD DO";A$
270 GOSUB 1890:V$=LEFT$(A$,3):GOSUB 1950:FOR R=1 TO 18:IF V$=V$(R) THEN 300 ELSE NEXT
280 IF V$="ORD" THEN 2210
290 PRINT"I DON'T KNOW HOW TO DO THAT.":GOTO260
300 IF V$="QUI" THEN 1650
310 V=R:FORR=1 TO LEN(A$):IF MID$(A$,R,1)=" " THEN 360 ELSE NEXT
320 IF V$="LOO" THEN 110
330 IF V$="BON" THEN 1670
340 IF V$="INV" THEN 1690
350 PRINT"PLEASE USE 2 WORD COMMANDS SO I CAN UNDERSTAND YOU.":GOTO260
360 V1$=MID$(A$,R+1,3):FORR=1 TO 49:IF V1$=N$(R) THEN 390 ELSE NEXT
370 IF V1$="NOR" OR V1$="SOU" OR V1$="EAS" OR V1$="WES" THEN 390
380 PRINT"I DON'T KNOW WHAT IT IS YOU ARE TALKING ABOUT.":GOTO 260
390 ON V GOTO 400,530,620,680,800,840,1040,1140,1300,1320,1360,1450,1490,1540,1600,1650,1670,1690
400 IF V1$="NOR" AND LO(LO,1)>0 THEN LO=LO(LO,1):GOTO 110
410 IF V1$="SOU" AND LO(LO,2)>0 THEN LO=LO(LO,2):GOTO 110
420 IF V1$="EAS" AND LO(LO,3)>0 THEN LO=LO(LO,3):GOTO 110
430 IF V1$="WES" AND LO(LO,4)>0 THEN LO=LO(LO,4):GOTO 110
440 IF V1$="NOR" OR V1$="SOU" OR V1$="EAS"ORV1$="WES"THEN520
450 GOSUB 20:IF Z9=2 THEN 210
460 IF R=8 THEN LO=5:GOTO 110
470 IF R=37 AND RO=1 AND LO=20 THEN LO=21:GOTO 110
480 IF R=10 THEN LO=11:GOTO 110
490 IF R=14 THEN LO=13:GOTO 110
500 IF R=33 THEN LO=3:GOTO 110
510 IF R=34 AND DO=1 THEN LO=9:GOTO 110
520 PRINT"I CAN'T GO THAT WAY AT THE MOMENT.":GOTO 210
530 GOSUB 20:IF Z9=2 THEN 210
540 IF R=2 OR R=3 OR R=4 OR R=6 OR R=15 OR R=16 OR R=20 OR R=21 OR R=22 OR R=23 OR R=25 OR R=26 OR R=27 OR R=28 OR R=30 OR R=31OR R=37 OR R=39 OR R=40 OR R=42 OR R=44 OR R=45 OR R=46 THEN 560
550 PRINT"I CAN'T CARRY THAT!":GOTO 210
560 IF N(R)=-1 PRINT"I ALREADY HAVE IT.":GOTO 210
570 IF N>=5 PRINT"I CAN'T CARRY ANYMORE.":GOTO210
580 PRINT"O.K.":N(R)=-1
590 IF V1$="PAI" AND PA=0 PRINT"SOMETHING FELL FROM THE FRAME!":N(31)=LO:PA=1
600 IF V1$="TEL" THEN TE=0
610 GOTO 210
620 FOR R=1 TO 46:IF V1$=N$(R) AND N(R)=-1 THEN 640 ELSE NEXT
630 PRINT"I DON'T SEEM TO BE CARRYING IT.":GOTO 210
640 IF R=30 PRINT"I DROPPED THE CUP BUT IT BROKE INTO SMALL PEICES.":PRINT"THE COFFEE SOAKED INTO THE GROUND.":N(30)=0:DR=0:GOTO 210
650 IF R=45 THEN GL=0
660 IF R=31 AND N(30)=-1 PRINT"O.K. I DROPPED IT.":PRINT"BUT IT FELL IN THE COFFEE!":N(31)=0:DR=1:GOTO 210
670 PRINT"O.K. I DROPPED IT.":N(R)=LO:GOTO 210
680 IF V1$="BUT" AND LO=3 AND DO=0 PRINT"THE DOORS OPEN WITH A WHOOSH!":DO=1:GOTO 210
690 IF N(46)=-1 AND V1$="BUT" THEN PRINT"I PUSH THE BUTTON ON THE BOX AND":GOTO 770
700 GOSUB 20
710 IF Z9=2 THEN 210
720 IF R=17 AND GL<>1 PRINT"THERE'S ELECTRICITY COURSING THRU THE SQUARE!":PRINT"I'M BEING ELECTROCUTED!":GOTO 2330
730 IF R=35 AND BU=0 PRINT"THE BUTTON ON THE WALL GOES IN .....":PRINT"CLICK! SOMETHING SEEMS DIFFFERENT NOW.":BU=1:GOTO 210
740 IF R=47 AND FL<>1 THEN LO(9,1)=3:FL=1:GOTO 790
750 IF R=48 AND FL<>2 THEN LO(9,1)=8:FL=2:GOTO 790
760 IF R=49 AND FL<>3 THEN LO(9,1)=10:FL=3:GOTO 790
770 IF N(46)=-1 AND (LO=6 OR LO=29)PRINT"THERE IS A BLINDING FLASH....":FORR=1TO3000:NEXT:LO=1:FL=1:LO(9,1)=3:GOTO 110
780 PRINT"NOTHING HAPPENS.":GOTO 210
790 PRINT"THE DOORS CLOSE AND I FEEL AS IF THE ROOM IS MOVING.":PRINT"SUDDENLY THE DOORS OPEN AGAIN.":FORR=1 TO 3000:NEXT:GOTO 110
800 GOSUB 20:IF Z9=2 THEN 210
810 IF R=18 AND GL<>1 PRINT"THE LEVER HAS ELECTRICITY COURSING THRU IT!":PRINT"I'M BEING ELECTROCUTED!":GOTO 2330
820 IF R=18 AND EL=0 PRINT"THE LEVER GOES ALL THE WAY UP AND CLICKS.":PRINT"SOMETHING SEEMS DIFFERENT NOW.":EL=1:GOTO 210
830 PRINT"NOTHING HAPPENS.":GOTO 210
840 REM ---- LOOKING AT OBJECTS ----
850 IF V1$="DRA" AND LO=5 PRINT"IT LOOKS FRAGILE.":GOTO210
860 GOSUB20:IFZ9=2THEN210
870 IF R=1 AND BA<>1 PRINT"THERE'S NO POWER FOR IT.":GOTO 210
880 IF R=1 AND TE<>1 PRINT"THERE'S NO T.V. TO WATCH ON.":GOTO 210
890 IF R=6 PRINT"IT LOOKS HEAVY.":GOTO 210
900 IF R=7 PRINT"IT'S LOCKED.":GOTO 210
910 IF R=13 PRINT"IT'S A VERY STRONG BAG.":GOTO 210
920 IF R=19 PRINT"I CAN SEE A LOCKED DRAWER IN IT.":GOTO 210
930 IF R=22 OR R=27 PRINT"THERE'S WRITING ON IT.":GOTO 210
940 IF R=34 AND DO=1 PRINT"THE DOORS ARE OPEN.":GOTO 210
950 IF R=24 PRINT"I CAN SEE A GLEAMING STONE IN IT.":GOTO 210
960 IF R=9 PRINT"THERE IS A SMALL SLIT NEAR THE DOOR.":GOTO 210
970 IF R=34 PRINT"THERE'S A BUTTON NEAR THE DOORS.":GOTO 210
980 IF R=41 AND BU=1 PRINT"THE SCREEN IS DARK.":GOTO210
990 IF R=41 PRINT"I SEE A METAL PIT 1000'S OF FEET DEEP ON ONE MONITOR.":PRINT"ON THE OTHER SIDE OF THE PIT,I SEE A LARGE HOOK.":GOTO 210
1000 IF R=43 AND BU=1 PRINT"THE SCREEN IS DARK.":GOTO210
1010 IF R=43 PRINT"I SEE A ROOM WITH A CASE ON A PEDESTAL IN IT.":GOTO210
1020 IF R=44 PRINT"I SEE A PICTURE OF A GRINNING JACKAL.":GOTO 210
1030 PRINT"I SEE NOTHING OF INTEREST.":GOTO 210
1040 GOSUB 20:IF Z9=2 THEN 210
1050 IF R<>3 AND R<>2 AND R<>4 AND R<>28 THEN PRINT"I CAN'T INSERT THAT!":GOTO 210
1060 INPUT"TELL ME,IN ONE WORD,INTO WHAT";A3$:A3$=LEFT$(A3$,3)
1070 R1=R:V1$=A3$:GOSUB20:IFZ9=2 THEN 210
1080 IF R1=3 AND R=1 THEN PRINT"O.K.":BA=1:N(3)=0:BA=1:GOTO210
1090 IF R1=4ANDR=50ANDDG<=0PRINT"THE GUARD WON'T LET ME!":GOTO210
1100 IF R1=2 AND R=1 THEN PRINT"O.K. THE TAPE IS IN THE RECORDER.":N(2)=0:TA=1:GOTO 210
1110 IF R1=4 AND R=50 THEN PRINT"POP! A SECTION OF THE WALL OPENS.....":PRINT"REVEALING SOMETHING VERY INTERESTING.":N(4)=0:N(5)=LO:GOTO210
1120 IF R1=28 AND R=29 PRINT"POP! A CUP OF COFFEE COMES OUT OF THE MACHINE.":N(28)=0:N(30)=LO:GOTO 210
1130 PRINT"NOTHING HAPPENED.":GOTO 210
1140 REM ---- OPENING OBJECTS ----
1150 IF V1$="DRA" AND LO=5 AND N(23)=0 PRINT"IT's STUCK.":GOTO210
1160 GOSUB20:IFZ9=2 THEN 210
1170 IF R=7 OR R=9 OR R=13 OR R=15 OR R=23 OR R=32 OR R=5 THEN 1190
1180 PRINT"I CAN'T OPEN THAT!":GOTO 210
1190 IF R=7 AND N(16)=-1 PRINT"O.K. I OPENED THE DOOR.":N(7)=0:N(8)=4:GOTO 210
1200 IF R=9 PRINT"I CAN'T. IT DOESN'T WORK.":GOTO 210
1210 IF R=13 AND N(16)=-1 PRINT"O.K. THE CLOSET IS OPENED.":N(13)=0:N(14)=14:GOTO 210
1220 IF R=15 PRINT"I CAN'T. IT'S TOO STRONG.":GOTO 210
1230 IF R=23 PRINT"IT'S STUCK.":GOTO 210
1240 IF R=32 AND N(28)=0 AND N(4)=0 AND SC=1 PRINT"I OPEN THE SCULPTURE.":PRINT"SOMETHING FALLS OUT.":N(28)=LO:N(4)=LO:GOTO 210
1250 IF R<>5 THEN 1290
1260 INPUT"WHAT'S THE COMBINATION";C$
1270 IF C$=CO$ THEN PRINT"THE DOOR IS SLOWLY OPENING.":N(5)=0:N(9)=0:N(10)=10:GOTO 210
1280 PRINT"YOU MUST HAVE THE WRONG COMBINATION OR YOU ARE NOT":PRINT"SAYING IT RIGHT.":GOTO 210
1290 PRINT"I CAN'T DO THAT......YET!":GOTO 210
1300 IF V1$="GLO" AND N(45)=-1 PRINT"O.K. I'M NOW WEARING THE GLOVES.":GL=1:GOTO 210
1310 PRINT"I CAN'T WEAR THAT!":GOTO 210
1320 IF V1$<>"SIG" AND V1$<>"NOT" THEN PRINT"I CAN'T READ THAT.":GOTO210
1330 GOSUB20:IFZ9=2THEN210
1340 IF R=22 PRINT"IT SAYS:":PRINTNA$;",":PRINT"  WE HAVE DISCOVERED ONE OF CHAOSES SECRET WORDS.":PRINT"IT IS: BOND-007- .TO BE USED IN A -TASTEFUL- SITUATION.":GOTO 210
1350 IF R=27 PRINT"IT SAYS: WATCH OUT! DANGEROUS!":GOTO 210
1360 IF V1$<>"REC" PRINT"I CAN'T START THAT.":GOTO 210
1370 GOSUB 20:IF Z9=2 THEN 210
1380 IF BA<>1 OR TE<>1 OR TA<>1 THEN 1440
1390 PRINT"THE RECORDER STARTS UP AND PRESENTS A SHORT MESSAGE:"
1400 PRINTNA$;","
1410 PRINT"WE HAVE UNCOVERED A NUMBER THAT MAY HELP YOU."
1420 PRINT"THAT NUMBER IS:";CO$;". PLEASE WATCH OUT FOR HIDDEN TRAPS."
1430 PRINT"ALSO, THERE IS SOMETHING IN THE SCULPTURE.":SC=1:GOTO210
1440 PRINT"NOTHING HAPPENED.":GOTO210
1450 IF V1$<>"DRA" THEN PRINT"I'M TRYING TO BREAK IT, BUT I CAN'T.":GOTO 210
1460 IF N(6)<>-1 PRINT"I CAN'T DO THAT YET.":GOTO210
1470 IF LO=5 PRINT"IT'S HARD....BUT I GOT IT. TWO THINGS FELL OUT.":N(3)=LO:N(22)=LO:N(23)=LO:GOTO 210
1480 PRINT"NOTHING HAPPENS.":GOTO 210
1490 GOSUB20:IFZ9=2THEN210
1500 IF R<>15 AND R<>24 THEN PRINT"I'M TRYING. IT DOESN'T WORK.":GOTO210
1510 IF N(25)<>-1 PRINT"I CAN'T DO THAT YET.":GOTO 210
1520 IF R=15 PRINT"RIP! THE BAG GOES TO PIECES, AND SOMETHING FALLS OUT!":N(15)=0:N(2)=LO:GOTO 210
1530 IF R=24 PRINT"I CUT THE CASE AND REACH IN TO PULL SOMETHING OUT.":N(26)=-1:GOTO 210
1540 IF V1$<>"ROP" THEN PRINT"I CAN'T THROW THAT.":GOTO 210
1550 IF N(37)<>-1 PRINT"I CAN'T DO THAT YET.":GOTO 210
1560 INPUT"TELL ME,IN ONE WORD,AT WHAT";A3$:A3$=LEFT$(A3$,3)
1570 IF A3$<>"HOO" PRINT"O.K. I THREW IT.":N(37)=LO:GOTO210
1580 IF LO<>20 PRINT"I CAN'T DO THAT YET.":GOTO 210
1590 PRINT"I THREW THE ROPE AND IT SNAGGED ON THE HOOK.":RO=1:N(37)=LO:GOTO 210
1600 IF V1$<>"TEL" THEN PRINT"I CAN'T CONNECT THAT.":GOTO210
1610 IF N(40)<>LO PRINT"I DON'T SEE THE TELEVISION HERE.":GOTO210
1620 IF TE=1 PRINT"I DID THAT ALREADY.":GOTO 210
1630 IF LO<>2 PRINT"I CAN'T DO THAT....YET!":GOTO210
1640 PRINT"O.K. THE T.V. IS CONNECTED.":TE=1:GOTO210
1650 PRINT"WHAT? YOU WOULD LEAVE ME HERE TO DIE ALONE?"
1660 PRINT"JUST FOR THAT, I'M GOING TO DESTROY THE GAME.":PRINT:PRINT:PRINT:PRINT"BOOOOOOOOOOOOM!":FORR=1 TO 5000:NEXT:STOP
1670 IF LO=14 PRINT"WHOOPS! A TRAP DOOR OPENED UNDERNEATH ME AND":PRINT"I FIND MYSELF FALLING.":FOR R=1 TO 4000:NEXT:LO=17:GOTO 110
1680 PRINT"NOTHING HAPPENED.":GOTO 210
1690 Z8=0:PRINT"WE ARE PRESENTLY CARRYING:":FOR R=1 TO 46
1700 IF N(R)<>-1 THEN 1730
1710 PRINTN1$(R);:IF R=45 AND GL=1PRINT". WHICH I'M WEARING."ELSE PRINT" "
1720 Z8=1
1730 NEXT
1740 IF Z8=0 PRINT"NOTHING"
1750 GOTO 210
1760 IF LO=3 AND N(39)=-1 PRINT"THE DOOR MAN LOOKS AT ME AND THEN THROWS ME OUT.":FOR R=1 TO 4000:NEXT:LO=1:GOTO110
1770 IF LO=10 AND N(42)<>-1 PRINT"THE GUARD LOOKS AT ME SUSPICIOUSLY, THEN THROWS ME BACK.":FORR=1TO4000:NEXT:LO=9:GOTO110
1780 IF LO=6 AND BU<>1 PRINT"SIRENS GO OFF ALL AROUND ME!":PRINT"GUARDS RUN IN AND SHOOT ME TO DEATH!":GOTO 2330
1790 IF LO=11 AND EL<>1 PRINT"THE FLOOR IS WIRED WITH ELECTRICITY!":PRINT"I'M BEING ELECTROCUTED!":GOTO2330
1800 IF LO=10 AND N(30)=-1 AND DR=1 PRINT"THE GUARD TAKES MY COFFEE":PRINT"AND FALLS TO SLEEP RIGHT AWAY.":DG=5+RND(10):N(11)=0:N(12)=10:DR=0:N(30)=0:GOTO240
1810 IF DG=0 PRINT"I HEAR A NOISE LIKE SOMEONE IS YAWNING.":N(11)=10:N(12)=0:GU=-2:DG=-1:GOTO240
1820 IF LO=1 AND N(26)=-1 PRINT"HURRAY! YOU'VE RECOVERED THE RUBY!":PRINT"YOU WIN!":GOTO2350
1830 IF T>375 PRINT"I THINK THEY ARE ON TO ME....I HEAR NOISES."
1840 IF T=400 PRINT"OH NO! THEY CAUGHT UP TO ME! HELP! THEY'RE PULLING OUT GUNS!":GOTO2330
1850 IF T=1 PRINT"WRITING ON THE WALL SAYS:":PRINT"IF YOU WANT INSTRUCTIONS TYPE:ORDERS PLEASE"
1860 IF LO=6 AND LO(6,2)<>0 PRINT"A SECRET DOOR SLAMS DOWN BEHIND ME!":LO(6,2)=0:GOTO240
1870 IF LO=10 AND GU=-2 PRINT"THE GUARD DRAWS HIS GUN AND SHOOTS ME!":GOTO 2330
1880 GOTO 240
1890 IFA$="N"THENA$="GO NORTH":RETURN
1900 IFA$="S"THENA$="GO SOUTH":RETURN
1910 IFA$="E"THENA$="GO EAST":RETURN
1920 IFA$="W"THENA$="GO WEST":RETURN
1930 IFA$="I"THENA$="INV":RETURN
1940 RETURN
1950 IF V$="WAL" OR V$="RUN" THEN V$="GO ":RETURN
1960 IF V$="TAK" OR V$="CAR" THEN V$="GET":RETURN
1970 IF V$="LEA" THEN V$="DRO":RETURN
1980 IF V$="PRE" THEN V$="PUS":RETURN
1990 IF V$="EXA" THEN V$="LOO":RETURN
2000 IF V$="PUT" THEN V$="INS":RETURN
2010 IF V$="UNL" THEN V$="OPE":RETURN
2020 IF V$="PLA" THEN V$="STA":RETURN
2030 IF V$="SMA" THEN V$="BRE":RETURN
2040 IF V$="ATT" THEN V$="CON":RETURN
2050 IF V$="LIS" THEN V$="INV":RETURN
2060 RETURN
2070 DATA A VIDEO CASSETTE RECORDER,REC,2,A VIDEO TAPE,TAP,0,A LARGE BATTERY,BAT,0,A BLANK CREDIT CARD,CAR,0,AN ELECTRONIC LOCK,LOC,0,AN ELABORATE PAPER WEIGHT,WEI,5,A LOCKED WOODEN DOOR,DOO,4,AN OPEN WOODEN DOOR,DOO,0,A SOLID LOOKING DOOR,DOO,10
2080 DATA AN OPEN DOOR,DOO,0,AN ALERT SECURITY GUARD,GUA,10,A SLEEPING SECURITY GUARD,GUA,0,A LOCKED MAINTENANCE CLOSET,CLO,14,A MAINTENANCE CLOSET,CLO,0,A PLASTIC BAG,BAG,13,AN OLDE FASHIONED KEY,KEY,9,A SMALL METAL SQUARE ON THE WALL,SQU,16
2090 DATA A LEVER ON THE SQUARE,LEV,16,AN OLD MAHOGANY DESK,DES,5,A BROOM,BRO,13,A DUSTPAN,DUS,13,A SPIRAL NOTEBOOK,NOT,0,A MAHOGANY DRAWER,DRA,0,A GLASS CASE ON A PEDESTAL,CAS,6,A RAZOR BLADE,BLA,27,A VERY LARGE RUBY,RUB,0,A SIGN ON THE SQUARE
2100 DATA SIG,16,A QUARTER,QUA,0,A COFFEE MACHINE,MAC,8,A CUP OF STEAMING HOT COFFEE,CUP,0,A SMALL CAPSULE,CAP,0,A LARGE SCULPTURE,SCU,3,A TALL OFFICE BUILDING,BUI,1,A PAIR OF SLIDING DOORS,DOO,3,A LARGE BUTTON ON THE WALL
2110 DATA BUT,29,A PANEL OF BUTTONS NUMBERED ONE THRU THREE,PAN,9,A STRONG NYLON ROPE,ROP,17,A LARGE HOOK WITH A ROPE HANGING FROM IT,HOO,21,A C.I.A. IDENTIFICATION BADGE,BAD,-1,A PORTABLE TELEVISION,TEL,7
2120 DATA A BANK OF MONITORS,MON,7,A CHAOS I.D. CARD
2130 DATA CAR,30,A BANK OF MONITORS,MON,19,A SMALL PAINTING,PAI,23,A PAIR OF RUBBER GLOVES,GLO,13,A BOX WITH A BUTTON ON IT,BOX,24,ONE,ONE,9,TWO,TWO,9,THREE,THR,9,SLIT,SLI,10,ON A BUSY STREET,0,0,0,0,IN A VISITOR'S ROOM,0,0,3,0
2140 DATA IN THE LOBBY OF THE BUILDING,1,0,4,2,IN A DINGY ANTE ROOM,0,0,0,3
2150 DATA IN THE COMPANY PRESIDENT'S OFFICE,0,0,0,4,IN A SMALL SOUND PROOFED CUBICLE,0,12,0,0,IN A SECURITY OFFICE,0,0,8,0,IN A SMALL HALLWAY,0,14,9,7,IN A SMALL ROOM,3,0,0,0,IN A SHORT CORRIDOR,0,15,0,9,IN A HALLWAY MADE OF METAL,0,0,12,10
2160 DATA IN A SMALL PLAIN ROOM,6,0,0,11
2170 DATA IN A MAINTENANCE CLOSET,0,0,14,0,IN A CAFETERIA,8,0,0,0,IN A SIDE CORRIDOR,10,0,16,0,IN A POWER GENERATOR ROOM,0,0,0,15,IN A SUB-BASEMENT BELOW THE CHUTE,0,0,18,0,IN THE ENTRANCE TO THE SECRET COMPLEX,0,20,19,17
2180 DATA IN A SECRET MONITORING ROOM,0,0,0,18,ON A LEDGE IN FRONT OF A METAL PIT 1000'S OF FEET DEEP,18,0,0,0,ON THE OTHER SIDE OF THE PIT,0,0,22,0,IN A LONG CORRIDOR,0,25,23,21,IN A LARGE ROOM,0,26,0,22,IN A SECRET LABORATORY,0,0,25,0
2190 DATA IN A NARROW CROSS CORRIDOR,22,0,0,24,IN A CROSS EXAMINATION ROOM,23,28,0,0,IN A SMALL BATHROOM,0,0,28,0,IN THE OFFICE OF THE CHIEF OF CHAOS,26,30,0,27,IN THE CHAOS CONTROL ROOM,0,0,30,0,NEAR THE END OF THE COMPLEX,28,0,0,29
2200 DATA "GO ",GET,DRO,PUS,PUL,LOO,INS,OPE,WEA,REA,STA,BRE,CUT,THR,CON,QUI,BON,INV
2210 CLS:PRINT"YOUR MISSION,";NA$;",IS TO RECOVER A RUBY THAT IS BEING"
2220 PRINT"USED IN TOP SECRET GOVERNMENT PROJECTS AS A PART IN A"
2230 PRINT"LASER PROJECTOR."
2240 PRINT"  YOU WILL HAVE A PARTNER WHO IS NOT TOO BRIGHT AND NEEDS"
2250 PRINT"YOU TO TELL HIM WHAT TO DO. USE TWO WORD COMMANDS LIKE:"
2260 PRINT:PRINT"	       GET NOTEBOOK   GO WEST  LOOK DOOR"
2270 PRINT:PRINT"SOME COMMANDS USE ONLY ONE WORD. EXAMPLE: INVENTORY"
2280 PRINT"  IF YOU WANT TO SEE CHANGES IN YOUR SURROUNDINGS TYPE: LOOK"
2290 PRINT"THE RUBY HAS BEEN CAPTURED BY A SECRET SPY RING KNOWN AS"
2300 PRINT"CHAOS. WE SUSPECT THEY ARE UNDER COVER SOMEWHERE IN THIS"
2310 PRINT"NEIGHBORHOOD. GOOD LUCK!"
2320 GOTO 240
2330 FOR R=1 TO 5000:NEXT:CLS:PRINT"I'M DEAD!"
2340 PRINT"YOU DIDN'T WIN."
2350 INPUT"WOULD YOU LIKE TO TRY AGAIN (Y/N)";Z$
2360 IF Z$="Y" THEN RUN
2370 'COPYRIGHT (C) CLOAD 1980
        HUGH LAMPERT
        110 LINDNER PL.
        MALVERNE, NY 11565
