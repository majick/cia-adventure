10 REM  CIA ADVENTURE
20 CLS:CLEAR5000:GOTO 50
30 Z9=0:FOR R=1 TO 50:IF V1$=N$(R) AND (N(R)=LO OR N(R)=-1) THEN Z9=1:RETURN
40 NEXT:PRINT"I DON'T SEE THAT HERE.":Z9=2:RETURN
50 PRINT"        C.I.A  ADVENTURE":PRINT"      BY   HUGH   LAMPERT"
60 DIM N1$(50),N$(50),N(50),V$(18),LO(30,4),LO$(30):FL=1:DR=0:DG=-1:T=0:EL=0:BU=0:DO=0:CA=0:BA=0:TE=0:GL=0
70 PA=0:RO=0:FOR R=1 TO 5:R1=RND(9):CO$=CO$+STR$(R1):NEXT:LO=1:A9$=">"+STRING$(62,"-")+"<"
80 R=LEN(CO$):CO$=RIGHT$(CO$,R-1)
90 FOR R=1 TO 50:READ N1$(R),N$(R),N(R):NEXT
100 FOR R=1 TO 30:READ LO$(R),LO(R,1),LO(R,2),LO(R,3),LO(R,4):NEXT
110 FOR R=1 TO 18:READ V$(R):NEXT
120 CLS
130 PRINT"WE ARE ";LO$(LO);"."
140 FOR R=1 TO 46:IF N(R)=LO PRINT"I CAN SEE ";N1$(R);"."
150 NEXT:B=0:FOR R=1 TO 4:IF LO(LO,R)>0 THEN B=1
160 NEXT:IF B=0 THEN 210
170 PRINT"WE COULD EASILY GO: ";:IF LO(LO,1)>0 PRINT"NORTH  ";
180 IF LO(LO,2)>0 PRINT"SOUTH  ";
190 IF LO(LO,3)>0 PRINT"EAST  ";
200 IF LO(LO,4)>0 PRINT"WEST  ";
210 PRINT:PRINT">";STRING$(62,"-");"<"
220 T=T+1:IF DG>0 THEN DG=DG-1
230 IF T=1 INPUT"ENTER YOUR NAME PARTNER";CM$:GOSUB2380:NA$=CM$
240 GOTO 1780
250 N=0:FOR R=1 TO 46:IF N(R)=-1 THEN N=N+1
260 NEXT
270 PRINT:INPUT"WHAT DO YOU THINK WE SHOULD DO";CM$:GOSUB2380:A$=CM$
280 IFA$="END"THENA$="QUIT"
290 GOSUB 1910:V$=LEFT$(A$,3):GOSUB 1970:FOR R=1 TO 18:IF V$=V$(R) THEN 320 ELSE NEXT
300 IF V$="ORD" THEN 2230
310 PRINT"I DON'T KNOW HOW TO DO THAT.":GOTO270
320 IFV$="QUI"ORV$="END" THEN 1670
330 V=R:FORR=1 TO LEN(A$):IF MID$(A$,R,1)=" " THEN 380 ELSE NEXT
340 IF V$="LOO" THEN 120
350 IF V$="BON" THEN 1690
360 IF V$="INV" THEN 1710
370 PRINT"PLEASE USE 2 WORD COMMANDS SO I CAN UNDERSTAND YOU.":GOTO270
380 V1$=MID$(A$,R+1,3):FORR=1 TO 49:IF V1$=N$(R) THEN 410 ELSE NEXT
390 IF V1$="NOR" OR V1$="SOU" OR V1$="EAS" OR V1$="WES" THEN 410
400 PRINT"I DON'T KNOW WHAT IT IS YOU ARE TALKING ABOUT.":GOTO 270
410 ON V GOTO 420,550,640,700,820,860,1060,1160,1320,1340,1380,1470,1510,1560,1620,1670,1690,1710
420 IF V1$="NOR" AND LO(LO,1)>0 THEN LO=LO(LO,1):GOTO 120
430 IF V1$="SOU" AND LO(LO,2)>0 THEN LO=LO(LO,2):GOTO 120
440 IF V1$="EAS" AND LO(LO,3)>0 THEN LO=LO(LO,3):GOTO 120
450 IF V1$="WES" AND LO(LO,4)>0 THEN LO=LO(LO,4):GOTO 120
460 IF V1$="NOR" OR V1$="SOU" OR V1$="EAS"ORV1$="WES"THEN540
470 GOSUB 30:IF Z9=2 THEN 220
480 IF R=8 THEN LO=5:GOTO 120
490 IF R=37 AND RO=1 AND LO=20 THEN LO=21:GOTO 120
500 IF R=10 THEN LO=11:GOTO 120
510 IF R=14 THEN LO=13:GOTO 120
520 IF R=33 THEN LO=3:GOTO 120
530 IF R=34 AND DO=1 THEN LO=9:GOTO 120
540 PRINT"I CAN'T GO THAT WAY AT THE MOMENT.":GOTO 220
550 GOSUB 30:IF Z9=2 THEN 220
560 IF R=2 OR R=3 OR R=4 OR R=6 OR R=15 OR R=16 OR R=20 OR R=21 OR R=22 OR R=23 OR R=25 OR R=26 OR R=27 OR R=28 OR R=30 OR R=31OR R=37 OR R=39 OR R=40 OR R=42 OR R=44 OR R=45 OR R=46 THEN 580
570 PRINT"I CAN'T CARRY THAT!":GOTO 220
580 IF N(R)=-1 PRINT"I ALREADY HAVE IT.":GOTO 220
590 IF N>=5 PRINT"I CAN'T CARRY ANYMORE.":GOTO220
600 PRINT"O.K.":N(R)=-1
610 IF V1$="PAI" AND PA=0 PRINT"SOMETHING FELL FROM THE FRAME!":N(31)=LO:PA=1
620 IF V1$="TEL" THEN TE=0
630 GOTO 220
640 FOR R=1 TO 46:IF V1$=N$(R) AND N(R)=-1 THEN 660 ELSE NEXT
650 PRINT"I DON'T SEEM TO BE CARRYING IT.":GOTO 220
660 IF R=30 PRINT"I DROPPED THE CUP BUT IT BROKE INTO SMALL PEICES.":PRINT"THE COFFEE SOAKED INTO THE GROUND.":N(30)=0:DR=0:GOTO 220
670 IF R=45 THEN GL=0
680 IF R=31 AND N(30)=-1 PRINT"O.K. I DROPPED IT.":PRINT"BUT IT FELL IN THE COFFEE!":N(31)=0:DR=1:GOTO 220
690 PRINT"O.K. I DROPPED IT.":N(R)=LO:GOTO 220
700 IF V1$="BUT" AND LO=3 AND DO=0 PRINT"THE DOORS OPEN WITH A WHOOSH!":DO=1:GOTO 220
710 IF N(46)=-1 AND V1$="BUT" THEN PRINT"I PUSH THE BUTTON ON THE BOX AND":GOTO 790
720 GOSUB 30
730 IF Z9=2 THEN 220
740 IF R=17 AND GL<>1 PRINT"THERE'S ELECTRICITY COURSING THRU THE SQUARE!":PRINT"I'M BEING ELECTROCUTED!":GOTO 2350
750 IF R=35 AND BU=0 PRINT"THE BUTTON ON THE WALL GOES IN .....":PRINT"CLICK! SOMETHING SEEMS DIFFFERENT NOW.":BU=1:GOTO 220
760 IF R=47 AND FL<>1 THEN LO(9,1)=3:FL=1:GOTO 810
770 IF R=48 AND FL<>2 THEN LO(9,1)=8:FL=2:GOTO 810
780 IF R=49 AND FL<>3 THEN LO(9,1)=10:FL=3:GOTO 810
790 IF N(46)=-1 AND (LO=6 OR LO=29)PRINT"THERE IS A BLINDING FLASH....":FORR=1TO3000:NEXT:LO=1:FL=1:LO(9,1)=3:GOTO 120
800 PRINT"NOTHING HAPPENS.":GOTO 220
810 PRINT"THE DOORS CLOSE AND I FEEL AS IF THE ROOM IS MOVING.":PRINT"SUDDENLY THE DOORS OPEN AGAIN.":FORR=1 TO 3000:NEXT:GOTO 120
820 GOSUB 30:IF Z9=2 THEN 220
830 IF R=18 AND GL<>1 PRINT"THE LEVER HAS ELECTRICITY COURSING THRU IT!":PRINT"I'M BEING ELECTROCUTED!":GOTO 2350
840 IF R=18 AND EL=0 PRINT"THE LEVER GOES ALL THE WAY UP AND CLICKS.":PRINT"SOMETHING SEEMS DIFFERENT NOW.":EL=1:GOTO 220
850 PRINT"NOTHING HAPPENS.":GOTO 220
860 REM ---- LOOKING AT OBJECTS ----
870 IF V1$="DRA" AND LO=5 PRINT"IT LOOKS FRAGILE.":GOTO220
880 GOSUB30:IFZ9=2THEN220
890 IF R=1 AND BA<>1 PRINT"THERE'S NO POWER FOR IT.":GOTO 220
900 IF R=1 AND TE<>1 PRINT"THERE'S NO T.V. TO WATCH ON.":GOTO 220
910 IF R=6 PRINT"IT LOOKS HEAVY.":GOTO 220
920 IF R=7 PRINT"IT'S LOCKED.":GOTO 220
930 IF R=13 PRINT"IT'S A VERY STRONG BAG.":GOTO 220
940 IF R=19 PRINT"I CAN SEE A LOCKED DRAWER IN IT.":GOTO 220
950 IF R=22 OR R=27 PRINT"THERE'S WRITING ON IT.":GOTO 220
960 IF R=34 AND DO=1 PRINT"THE DOORS ARE OPEN.":GOTO 220
970 IF R=24 PRINT"I CAN SEE A GLEAMING STONE IN IT.":GOTO 220
980 IF R=9 PRINT"THERE IS A SMALL SLIT NEAR THE DOOR.":GOTO 220
990 IF R=34 PRINT"THERE'S A BUTTON NEAR THE DOORS.":GOTO 220
1000 IF R=41 AND BU=1 PRINT"THE SCREEN IS DARK.":GOTO220
1010 IF R=41 PRINT"I SEE A METAL PIT 1000'S OF FEET DEEP ON ONE MONITOR.":PRINT"ON THE OTHER SIDE OF THE PIT,I SEE A LARGE HOOK.":GOTO 220
1020 IF R=43 AND BU=1 PRINT"THE SCREEN IS DARK.":GOTO220
1030 IF R=43 PRINT"I SEE A ROOM WITH A CASE ON A PEDESTAL IN IT.":GOTO220
1040 IF R=44 PRINT"I SEE A PICTURE OF A GRINNING JACKAL.":GOTO 220
1050 PRINT"I SEE NOTHING OF INTEREST.":GOTO 220
1060 GOSUB 30:IF Z9=2 THEN 220
1070 IF R<>3 AND R<>2 AND R<>4 AND R<>28 THEN PRINT"I CAN'T INSERT THAT!":GOTO 220
1080 INPUT"TELL ME,IN ONE WORD,INTO WHAT";A3$:A3$=LEFT$(A3$,3)
1090 R1=R:V1$=A3$:GOSUB30:IFZ9=2 THEN 220
1100 IF R1=3 AND R=1 THEN PRINT"O.K.":BA=1:N(3)=0:BA=1:GOTO220
1110 IF R1=4ANDR=50ANDDG<=0PRINT"THE GUARD WON'T LET ME!":GOTO220
1120 IF R1=2 AND R=1 THEN PRINT"O.K. THE TAPE IS IN THE RECORDER.":N(2)=0:TA=1:GOTO 220
1130 IF R1=4 AND R=50 THEN PRINT"POP! A SECTION OF THE WALL OPENS.....":PRINT"REVEALING SOMETHING VERY INTERESTING.":N(4)=0:N(5)=LO:GOTO220
1140 IF R1=28 AND R=29 PRINT"POP! A CUP OF COFFEE COMES OUT OF THE MACHINE.":N(28)=0:N(30)=LO:GOTO 220
1150 PRINT"NOTHING HAPPENED.":GOTO 220
1160 REM ---- OPENING OBJECTS ----
1170 IF V1$="DRA" AND LO=5 AND N(23)=0 PRINT"IT's STUCK.":GOTO220
1180 GOSUB30:IFZ9=2 THEN 220
1190 IF R=7 OR R=9 OR R=13 OR R=15 OR R=23 OR R=32 OR R=5 THEN 1210
1200 PRINT"I CAN'T OPEN THAT!":GOTO 220
1210 IF R=7 AND N(16)=-1 PRINT"O.K. I OPENED THE DOOR.":N(7)=0:N(8)=4:GOTO 220
1220 IF R=9 PRINT"I CAN'T. IT DOESN'T WORK.":GOTO 220
1230 IF R=13 AND N(16)=-1 PRINT"O.K. THE CLOSET IS OPENED.":N(13)=0:N(14)=14:GOTO 220
1240 IF R=15 PRINT"I CAN'T. IT'S TOO STRONG.":GOTO 220
1250 IF R=23 PRINT"IT'S STUCK.":GOTO 220
1260 IF R=32 AND N(28)=0 AND N(4)=0 AND SC=1 PRINT"I OPEN THE SCULPTURE.":PRINT"SOMETHING FALLS OUT.":N(28)=LO:N(4)=LO:GOTO 220
1270 IF R<>5 THEN 1310
1280 INPUT"WHAT'S THE COMBINATION";C$
1290 IF C$=CO$ THEN PRINT"THE DOOR IS SLOWLY OPENING.":N(5)=0:N(9)=0:N(10)=10:GOTO 220
1300 PRINT"YOU MUST HAVE THE WRONG COMBINATION OR YOU ARE NOT":PRINT"SAYING IT RIGHT.":GOTO 220
1310 PRINT"I CAN'T DO THAT......YET!":GOTO 220
1320 IF V1$="GLO" AND N(45)=-1 PRINT"O.K. I'M NOW WEARING THE GLOVES.":GL=1:GOTO 220
1330 PRINT"I CAN'T WEAR THAT!":GOTO 220
1340 IF V1$<>"SIG" AND V1$<>"NOT" THEN PRINT"I CAN'T READ THAT.":GOTO220
1350 GOSUB30:IFZ9=2THEN220
1360 IF R=22 PRINT"IT SAYS:":PRINTNA$;",":PRINT"  WE HAVE DISCOVERED ONE OF CHAOSES SECRET WORDS.":PRINT"IT IS: BOND-007- .TO BE USED IN A -TASTEFUL- SITUATION.":GOTO 220
1370 IF R=27 PRINT"IT SAYS: WATCH OUT! DANGEROUS!":GOTO 220
1380 IF V1$<>"REC" PRINT"I CAN'T START THAT.":GOTO 220
1390 GOSUB 30:IF Z9=2 THEN 220
1400 IF BA<>1 OR TE<>1 OR TA<>1 THEN 1460
1410 PRINT"THE RECORDER STARTS UP AND PRESENTS A SHORT MESSAGE:"
1420 PRINTNA$;","
1430 PRINT"WE HAVE UNCOVERED A NUMBER THAT MAY HELP YOU."
1440 PRINT"THAT NUMBER IS:";CO$;". PLEASE WATCH OUT FOR HIDDEN TRAPS."
1450 PRINT"ALSO, THERE IS SOMETHING IN THE SCULPTURE.":SC=1:GOTO220
1460 PRINT"NOTHING HAPPENED.":GOTO220
1470 IF V1$<>"DRA" THEN PRINT"I'M TRYING TO BREAK IT, BUT I CAN'T.":GOTO 220
1480 IF N(6)<>-1 PRINT"I CAN'T DO THAT YET.":GOTO220
1490 IF LO=5 PRINT"IT'S HARD....BUT I GOT IT. TWO THINGS FELL OUT.":N(3)=LO:N(22)=LO:N(23)=LO:GOTO 220
1500 PRINT"NOTHING HAPPENS.":GOTO 220
1510 GOSUB30:IFZ9=2THEN220
1520 IF R<>15 AND R<>24 THEN PRINT"I'M TRYING. IT DOESN'T WORK.":GOTO220
1530 IF N(25)<>-1 PRINT"I CAN'T DO THAT YET.":GOTO 220
1540 IF R=15 PRINT"RIP! THE BAG GOES TO PIECES, AND SOMETHING FALLS OUT!":N(15)=0:N(2)=LO:GOTO 220
1550 IF R=24 PRINT"I CUT THE CASE AND REACH IN TO PULL SOMETHING OUT.":N(26)=-1:GOTO 220
1560 IF V1$<>"ROP" THEN PRINT"I CAN'T THROW THAT.":GOTO 220
1570 IF N(37)<>-1 PRINT"I CAN'T DO THAT YET.":GOTO 220
1580 INPUT"TELL ME,IN ONE WORD,AT WHAT";A3$:A3$=LEFT$(A3$,3)
1590 IF A3$<>"HOO" PRINT"O.K. I THREW IT.":N(37)=LO:GOTO220
1600 IF LO<>20 PRINT"I CAN'T DO THAT YET.":GOTO 220
1610 PRINT"I THREW THE ROPE AND IT SNAGGED ON THE HOOK.":RO=1:N(37)=LO:GOTO 220
1620 IF V1$<>"TEL" THEN PRINT"I CAN'T CONNECT THAT.":GOTO220
1630 IF N(40)<>LO PRINT"I DON'T SEE THE TELEVISION HERE.":GOTO220
1640 IF TE=1 PRINT"I DID THAT ALREADY.":GOTO 220
1650 IF LO<>2 PRINT"I CAN'T DO THAT....YET!":GOTO220
1660 PRINT"O.K. THE T.V. IS CONNECTED.":TE=1:GOTO220
1670 PRINT"WHAT? YOU WOULD LEAVE ME HERE TO DIE ALONE?"
1680 PRINT"JUST FOR THAT, I'M GOING TO DESTROY THE GAME.":PRINT:PRINT:PRINT:PRINT"BOOOOOOOOOOOOM!":GOTO2370
1690 IF LO=14 PRINT"WHOOPS! A TRAP DOOR OPENED UNDERNEATH ME AND":PRINT"I FIND MYSELF FALLING.":FOR R=1 TO 4000:NEXT:LO=17:GOTO 120
1700 PRINT"NOTHING HAPPENED.":GOTO 220
1710 Z8=0:PRINT"WE ARE PRESENTLY CARRYING:":FOR R=1 TO 46
1720 IF N(R)<>-1 THEN 1750
1730 PRINTN1$(R);:IF R=45 AND GL=1PRINT". WHICH I'M WEARING."ELSE PRINT" "
1740 Z8=1
1750 NEXT
1760 IF Z8=0 PRINT"NOTHING"
1770 GOTO 220
1780 IF LO=3 AND N(39)=-1 PRINT"THE DOOR MAN LOOKS AT ME AND THEN THROWS ME OUT.":FOR R=1 TO 4000:NEXT:LO=1:GOTO120
1790 IF LO=10 AND N(42)<>-1 PRINT"THE GUARD LOOKS AT ME SUSPICIOUSLY, THEN THROWS ME BACK.":FORR=1TO4000:NEXT:LO=9:GOTO120
1800 IF LO=6 AND BU<>1 PRINT"SIRENS GO OFF ALL AROUND ME!":PRINT"GUARDS RUN IN AND SHOOT ME TO DEATH!":GOTO 2350
1810 IF LO=11 AND EL<>1 PRINT"THE FLOOR IS WIRED WITH ELECTRICITY!":PRINT"I'M BEING ELECTROCUTED!":GOTO2350
1820 IF LO=10 AND N(30)=-1 AND DR=1 PRINT"THE GUARD TAKES MY COFFEE":PRINT"AND FALLS TO SLEEP RIGHT AWAY.":DG=5+RND(10):N(11)=0:N(12)=10:DR=0:N(30)=0:GOTO250
1830 IF DG=0 PRINT"I HEAR A NOISE LIKE SOMEONE IS YAWNING.":N(11)=10:N(12)=0:GU=-2:DG=-1:GOTO250
1840 IF LO=1 AND N(26)=-1 PRINT"HURRAY! YOU'VE RECOVERED THE RUBY!":PRINT"YOU WIN!":GOTO2370
1850 IF T>375 PRINT"I THINK THEY ARE ON TO ME....I HEAR NOISES."
1860 IF T=400 PRINT"OH NO! THEY CAUGHT UP TO ME! HELP! THEY'RE PULLING OUT GUNS!":GOTO2350
1870 IF T=1 PRINT"WRITING ON THE WALL SAYS:":PRINT"IF YOU WANT INSTRUCTIONS TYPE:ORDERS PLEASE"
1880 IF LO=6 AND LO(6,2)<>0 PRINT"A SECRET DOOR SLAMS DOWN BEHIND ME!":LO(6,2)=0:GOTO250
1890 IF LO=10 AND GU=-2 PRINT"THE GUARD DRAWS HIS GUN AND SHOOTS ME!":GOTO 2350
1900 GOTO 250
1910 IFA$="N"THENA$="GO NORTH":RETURN
1920 IFA$="S"THENA$="GO SOUTH":RETURN
1930 IFA$="E"THENA$="GO EAST":RETURN
1940 IFA$="W"THENA$="GO WEST":RETURN
1950 IFA$="I"THENA$="INV":RETURN
1960 RETURN
1970 IF V$="WAL" OR V$="RUN" THEN V$="GO ":RETURN
1980 IF V$="TAK" OR V$="CAR" THEN V$="GET":RETURN
1990 IF V$="LEA" THEN V$="DRO":RETURN
2000 IF V$="PRE" THEN V$="PUS":RETURN
2010 IF V$="EXA" THEN V$="LOO":RETURN
2020 IF V$="PUT" THEN V$="INS":RETURN
2030 IF V$="UNL" THEN V$="OPE":RETURN
2040 IF V$="PLA" THEN V$="STA":RETURN
2050 IF V$="SMA" THEN V$="BRE":RETURN
2060 IF V$="ATT" THEN V$="CON":RETURN
2070 IF V$="LIS" THEN V$="INV":RETURN
2080 RETURN
2090 DATA A VIDEO CASSETTE RECORDER,REC,2,A VIDEO TAPE,TAP,0,A LARGE BATTERY,BAT,0,A BLANK CREDIT CARD,CAR,0,AN ELECTRONIC LOCK,LOC,0,AN ELABORATE PAPER WEIGHT,WEI,5,A LOCKED WOODEN DOOR,DOO,4,AN OPEN WOODEN DOOR,DOO,0,A SOLID LOOKING DOOR,DOO,10
2100 DATA AN OPEN DOOR,DOO,0,AN ALERT SECURITY GUARD,GUA,10,A SLEEPING SECURITY GUARD,GUA,0,A LOCKED MAINTENANCE CLOSET,CLO,14,A MAINTENANCE CLOSET,CLO,0,A PLASTIC BAG,BAG,13,AN OLDE FASHIONED KEY,KEY,9,A SMALL METAL SQUARE ON THE WALL,SQU,16
2110 DATA A LEVER ON THE SQUARE,LEV,16,AN OLD MAHOGANY DESK,DES,5,A BROOM,BRO,13,A DUSTPAN,DUS,13,A SPIRAL NOTEBOOK,NOT,0,A MAHOGANY DRAWER,DRA,0,A GLASS CASE ON A PEDESTAL,CAS,6,A RAZOR BLADE,BLA,27,A VERY LARGE RUBY,RUB,0,A SIGN ON THE SQUARE
2120 DATA SIG,16,A QUARTER,QUA,0,A COFFEE MACHINE,MAC,8,A CUP OF STEAMING HOT COFFEE,CUP,0,A SMALL CAPSULE,CAP,0,A LARGE SCULPTURE,SCU,3,A TALL OFFICE BUILDING,BUI,1,A PAIR OF SLIDING DOORS,DOO,3,A LARGE BUTTON ON THE WALL
2130 DATA BUT,29,A PANEL OF BUTTONS NUMBERED ONE THRU THREE,PAN,9,A STRONG NYLON ROPE,ROP,17,A LARGE HOOK WITH A ROPE HANGING FROM IT,HOO,21,A C.I.A. IDENTIFICATION BADGE,BAD,-1,A PORTABLE TELEVISION,TEL,7
2140 DATA A BANK OF MONITORS,MON,7,A CHAOS I.D. CARD
2150 DATA CAR,30,A BANK OF MONITORS,MON,19,A SMALL PAINTING,PAI,23,A PAIR OF RUBBER GLOVES,GLO,13,A BOX WITH A BUTTON ON IT,BOX,24,ONE,ONE,9,TWO,TWO,9,THREE,THR,9,SLIT,SLI,10,ON A BUSY STREET,0,0,0,0,IN A VISITOR'S ROOM,0,0,3,0
2160 DATA IN THE LOBBY OF THE BUILDING,1,0,4,2,IN A DINGY ANTE ROOM,0,0,0,3
2170 DATA IN THE COMPANY PRESIDENT'S OFFICE,0,0,0,4,IN A SMALL SOUND PROOFED CUBICLE,0,12,0,0,IN A SECURITY OFFICE,0,0,8,0,IN A SMALL HALLWAY,0,14,9,7,IN A SMALL ROOM,3,0,0,0,IN A SHORT CORRIDOR,0,15,0,9,IN A HALLWAY MADE OF METAL,0,0,12,10
2180 DATA IN A SMALL PLAIN ROOM,6,0,0,11
2190 DATA IN A MAINTENANCE CLOSET,0,0,14,0,IN A CAFETERIA,8,0,0,0,IN A SIDE CORRIDOR,10,0,16,0,IN A POWER GENERATOR ROOM,0,0,0,15,IN A SUB-BASEMENT BELOW THE CHUTE,0,0,18,0,IN THE ENTRANCE TO THE SECRET COMPLEX,0,20,19,17
2200 DATA IN A SECRET MONITORING ROOM,0,0,0,18,ON A LEDGE IN FRONT OF A METAL PIT 1000'S OF FEET DEEP,18,0,0,0,ON THE OTHER SIDE OF THE PIT,0,0,22,0,IN A LONG CORRIDOR,0,25,23,21,IN A LARGE ROOM,0,26,0,22,IN A SECRET LABORATORY,0,0,25,0
2210 DATA IN A NARROW CROSS CORRIDOR,22,0,0,24,IN A CROSS EXAMINATION ROOM,23,28,0,0,IN A SMALL BATHROOM,0,0,28,0,IN THE OFFICE OF THE CHIEF OF CHAOS,26,30,0,27,IN THE CHAOS CONTROL ROOM,0,0,30,0,NEAR THE END OF THE COMPLEX,28,0,0,29
2220 DATA "GO ",GET,DRO,PUS,PUL,LOO,INS,OPE,WEA,REA,STA,BRE,CUT,THR,CON,QUI,BON,INV
2230 CLS:PRINT"YOUR MISSION,";NA$;",IS TO RECOVER A RUBY THAT IS BEING"
2240 PRINT"USED IN TOP SECRET GOVERNMENT PROJECTS AS A PART IN A"
2250 PRINT"LASER PROJECTOR."
2260 PRINT"  YOU WILL HAVE A PARTNER WHO IS NOT TOO BRIGHT AND NEEDS"
2270 PRINT"YOU TO TELL HIM WHAT TO DO. USE TWO WORD COMMANDS LIKE:"
2280 PRINT:PRINT"	       GET NOTEBOOK   GO WEST  LOOK DOOR"
2290 PRINT:PRINT"SOME COMMANDS USE ONLY ONE WORD. EXAMPLE: INVENTORY"
2300 PRINT"  IF YOU WANT TO SEE CHANGES IN YOUR SURROUNDINGS TYPE: LOOK"
2310 PRINT"THE RUBY HAS BEEN CAPTURED BY A SECRET SPY RING KNOWN AS"
2320 PRINT"CHAOS. WE SUSPECT THEY ARE UNDER COVER SOMEWHERE IN THIS"
2330 PRINT"NEIGHBORHOOD. GOOD LUCK!"
2340 GOTO 250
2350 FOR R=1 TO 5000:NEXT:CLS:PRINT"I'M DEAD!"
2360 PRINT"YOU DIDN'T WIN."
2370 END
2380 IFLEN(CM$)=0ORLEFT$(CM$,1)<CHR$(33)THENRETURNELSEFORXX%=1TOLEN(CM$)
2390 IFASC(MID$(CM$,XX%,1))>96THENMID$(CM$,XX%,1)=CHR$(ASC(MID$(CM$,XX%,1))-32)
2400 NEXT XX%
2410 RETURN
